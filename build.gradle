plugins {
  id "com.moowork.node" version "1.2.0"
  id 'org.springframework.boot' version '2.0.4.RELEASE'
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

jar {
  baseName = 'life-booster'
  version = '0.0.2-SNAPSHOT'
}

repositories {
  jcenter()
  mavenLocal()
  maven { url 'https://repo.spring.io/snapshot' }
  maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
  implementation('org.apache.httpcomponents:httpclient')
  implementation('org.visola.spring.security:spring-security-token-filter-spring-boot-starter:2.0')
  implementation('org.springframework.boot:spring-boot-starter-security')
  implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
  implementation('org.springframework.boot:spring-boot-starter-web')
}

sourceSets {
  main {
    resources {
      srcDir "${buildDir}/frontend"
    }
  }
}

task bundleFrontend (type: NpmTask) {
  dependsOn npmInstall
  args = ["run", "bundleProd"]
  inputs.dir file("src/frontend")
  outputs.dir file("${buildDir}/frontend/static")
}

processResources.dependsOn bundleFrontend